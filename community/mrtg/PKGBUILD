# $Id: PKGBUILD 146521 2012-01-12 10:46:44Z giovanni $
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@yahoo.com.ar>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - bump for libpng rebuild, remove when updated in ABS

pkgname=mrtg
pkgver=2.17.4
pkgrel=2
pkgdesc="Multi Router Traffic Grapher."
arch=('i686' 'x86_64')
url="http://oss.oetiker.ch/mrtg/"
license=('GPL')
depends=('gd' 'perl')
source=("http://oss.oetiker.ch/mrtg/pub/${pkgname}-${pkgver}.tar.gz")
md5sums=('197565e97f97212eef7b414463c8d40c')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed 's|LD_RUN_PATH=$(LD_RUN_PATH) ||g' -i Makefile.in
  ./configure --prefix=/usr

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make prefix=${pkgdir}/usr install
}
