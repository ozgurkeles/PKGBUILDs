# Maintainer: CruX <crux@project-insanity.org>
# Contributor: pepedog at archlinuxarm dot com

buildarch=4
pkgname=libfslvpuwrap
pkgver=3.10.17_1.0.0
_pkgver=1.0.46
pkgrel=3
pkgdesc="Wrapper library for the freescale proprietary VPU extensions"
url="https://community.freescale.com/docs/DOC-95560"
arch=('armv7h')
license=('proprietary')
depends=("imx-vpu=$pkgver")
source=("http://www.freescale.com/lgfiles/NMG/MAD/YOCTO/${pkgname}-${_pkgver}.bin"
	'0001-vpu_wrapper-fix-tests-of-return-value-from-IOGetVirt.patch')
md5sums=('1f50110cb6de8ebf767fb9c5f8baf20d'
         '478d6d6bb5043cdcdced42fcd9fec48d')

prepare() {
  cd "${srcdir}"
  #library is packed as binary, accept EULA
  sh ${pkgname}-${_pkgver}.bin --force --auto-accept
  sed -n '/EOEULA/,/EOEULA/p' ${pkgname}-${_pkgver}.bin | grep -v EOEULA > LICENSE.$pkgname

  cd "${pkgname}-${_pkgver}"
  patch -Np1 < ../0001-vpu_wrapper-fix-tests-of-return-value-from-IOGetVirt.patch
}

build() {
  cd "${pkgname}-${_pkgver}"
  export CFLAGS="$CFLAGS -O2 -I/opt/fsl/include"
  export CPPFLAGS="$CPPFLAGS -O2 -I/opt/fsl/include"
  export LDFLAGS="$LDFLAGS -L/opt/fsl/lib"
  ./autogen.sh --prefix=/opt/fsl
  make all
}

package() {
  cd "${pkgname}-${_pkgver}"
  make DESTDIR=${pkgdir} install
  mkdir -p "${pkgdir}/opt/fsl/licenses"
  cp "${srcdir}/LICENSE.$pkgname" "$pkgdir/opt/fsl/licenses"
  mkdir -p "${pkgdir}/etc/ld.so.conf.d"
  echo "/opt/fsl/lib" > "${pkgdir}/etc/ld.so.conf.d/$pkgname.conf"
}

# vim:set ts=2 sw=2 et:
