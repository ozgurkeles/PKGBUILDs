# U-Boot: UDOO
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=4

pkgbase=uboot-udoo
pkgname=('uboot-udoo-dual' 'uboot-udoo-quad')
pkgver=2014.04
pkgrel=1
arch=('armv7h')
url="http://git.denx.de/u-boot.git/"
license=('GPL')
source=("ftp://ftp.denx.de/pub/u-boot/u-boot-${pkgver}.tar.bz2"
        'http://raw.github.com/eewiki/u-boot-patches/master/v${pkgver}/0001-udoo-uEnv.txt-bootz-n-fixes.patch'
        'alarm.patch')
md5sums=('6d2116d1385a66e9a59742caa9d62a54'
         'd796b672643d92f6b43b4ba74e8a244f'
         '119804f59c0b392c70bcdc67dc220ae0')

prepare() {
  cd u-boot-${pkgver}

  patch -p1 -i ../0001-udoo-uEnv.txt-bootz-n-fixes.patch
  patch -p1 -i ../alarm.patch
}

build() {
  cd u-boot-${pkgver}
  
  unset CFLAGS CXXFLAGS LDFLAGS
  
  mkdir bins
  for i in dl quad; do
    make distclean
    make udoo_${i}_config
    make
    mv u-boot.imx bins/u-boot-${i}.imx
  done
}

package_uboot-udoo-dual() {
  pkgdesc="U-Boot for UDOO Dual"
  conflicts=('uboot-udoo-quad')
  install=${pkgbase}.install

  mkdir -p "${pkgdir}"/boot
  cp u-boot-${pkgver}/bins/u-boot-dl.imx "${pkgdir}"/boot/u-boot.imx
}

package_uboot-udoo-quad() {
  pkgdesc="U-Boot for UDOO Quad"
  conflicts=('uboot-udoo-dual')
  install=${pkgbase}.install

  mkdir -p "${pkgdir}"/boot
  cp u-boot-${pkgver}/bins/u-boot-quad.imx "${pkgdir}"/boot/u-boot.imx
}
md5sums=('6d2116d1385a66e9a59742caa9d62a54'
         '9a9e4247a28d6eb936746be68a2c524e'
         '51ca2a4f726bd02943d10c4d91ef07be')
