# U-Boot: Boundary Devices Nitrogen6X/Sabre Lite
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=4

pkgname=uboot-boundary
pkgver=2014.04
pkgrel=2
arch=('armv7h')
pkgdesc="U-Boot for Nitrogen6X/Sabre Lite"
url="https://github.com/boundarydevices/u-boot-imx6/tree/production"
license=('GPL')
makedepends=('uboot-mkimage')
backup=('6x_bootscript')
_commit=aed9475361820a65e37ed936c833322cbbc0f2b5
source=("https://github.com/boundarydevices/u-boot-imx6/archive/${_commit}.tar.gz"
        '6x_bootscript'
        'alarm.patch')
md5sums=('652aa9b6be19b76d1cd2939a3aba8562'
         '4c78341ff205c4e99f428385869c22e0'
         '9c8304b11ad58a9b9604d89d076856dd')

prepare() {
  cd u-boot-imx6-${_commit}
  patch -p1 -i "${srcdir}"/alarm.patch
}

build() {
  cd u-boot-imx6-${_commit}
  
  unset CFLAGS
  unset CXXFLAGS
  
  make distclean
  make nitrogen6q_config
  make
}

package() {
  cd u-boot-imx6-${_commit}

  mkdir -p "${pkgdir}"/boot
  cp u-boot.imx "${pkgdir}"/boot
  mkimage -A arm -O linux -T script -C none -a 0 -e 0 -n "boot script" -d ../6x_bootscript ${pkgdir}/6x_bootscript
}
