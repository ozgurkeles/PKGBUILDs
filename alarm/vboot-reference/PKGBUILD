# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org
# Contributor: Justin Settle <jus10 at partlycloudy dot org>

buildarch=4

pkgbase=vboot-reference
pkgname=(cgpt vboot-utils)
pkgver=r1135.d81a326
pkgrel=1
arch=('armv7h')
url="https://chromium.googlesource.com/chromiumos/platform/vboot_reference/+/master"
license=('custom:chromiumos')
makedepends=('libyaml' 'git')
source=("git+https://chromium.googlesource.com/chromiumos/platform/vboot_reference")
md5sums=('SKIP')

pkgver() {
  cd vboot_reference
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd vboot_reference
  # don't statically link
  sed -i '/LDFLAGS += -static/d' Makefile
  # remove -Werror to prevent build failure
  sed -i 's/-Werror//g' Makefile
}

build() {
  cd vboot_reference
  ARCH=arm make cgpt utils
}

package_cgpt() {
  pkgdesc="Chromium OS cgpt partition tool"

  cd vboot_reference
  install -Dm755 build/cgpt/cgpt "${pkgdir}/usr/sbin/cgpt"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_vboot-utils() {
  pkgdesc="Chromium OS vboot utilities"

  cd vboot_reference
  install -d "${pkgdir}/usr/sbin"
  install -m755 build/utility/vbutil_{key{,block},kernel,firmware} "${pkgdir}/usr/sbin"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
