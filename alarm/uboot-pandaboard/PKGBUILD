# U-Boot: Pandaboard and Pandaboard ES
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=4

pkgname=uboot-pandaboard
pkgver=2013.04
pkgrel=1
pkgdesc="U-Boot for Pandaboard and Pandaboard ES"
arch=('armv7h')
url="http://git.denx.de/u-boot.git/"
license=('GPL')
makedepends=('git')
source=("uboot::git://git.denx.de/u-boot.git#tag=v${pkgver}"
        'http://raw.github.com/eewiki/u-boot-patches/master/v${pkgver}/0001-omap4_common-uEnv.txt-bootz-n-fixes.patch'
        'alarm.patch'
        'uEnv.txt')
md5sums=('SKIP'
         '7472e6e0c10c59ca53442bb85a33278e'
         '2a411ced08f8c536b76b47df64a36d17'
         '9d4d647761d7e5dac7b6368970c74eb5')

prepare() {
  cd uboot
  patch -p1 -i "${srcdir}"/0001-omap4_common-uEnv.txt-bootz-n-fixes.patch
  patch -p1 -i "${srcdir}"/alarm.patch
}

build() {
  cd uboot
  make distclean 
  make omap4_panda_config
  make
}

package() {
  cd uboot
  mkdir -p "${pkgdir}"/boot
  cp MLO u-boot.img "${srcdir}"/uEnv.txt "${pkgdir}"/boot
}
