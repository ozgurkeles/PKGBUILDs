# U-Boot: Pandaboard and Pandaboard ES
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=4

pkgname=uboot-pandaboard
pkgver=2014.04
pkgrel=1
pkgdesc="U-Boot for Pandaboard and Pandaboard ES"
arch=('armv7h')
url="http://git.denx.de/u-boot.git/"
license=('GPL')
source=("ftp://ftp.denx.de/pub/u-boot/u-boot-${pkgver}.tar.bz2"
        'http://raw.github.com/eewiki/u-boot-patches/master/v${pkgver}/0001-omap4_common-uEnv.txt-bootz-n-fixes.patch'
        'alarm.patch'
        'uEnv.txt')
md5sums=('6d2116d1385a66e9a59742caa9d62a54'
         'd796b672643d92f6b43b4ba74e8a244f'
         '119804f59c0b392c70bcdc67dc220ae0'
         '76589e692f164699f5d8c7202af1f4fc')

prepare() {
  cd u-boot-${pkgver}

  patch -Np1 -i "${srcdir}"/0001-omap4_common-uEnv.txt-bootz-n-fixes.patch
  patch -Np1 -i "${srcdir}"/alarm.patch
}

build() {
  cd u-boot-${pkgver}

  unset LDFLAGS

  make distclean 
  make omap4_panda_config
  make
}

package() {
  cd u-boot-${pkgver}

  mkdir -p "${pkgdir}"/boot
  cp MLO u-boot.img "${srcdir}"/uEnv.txt "${pkgdir}"/boot
}
