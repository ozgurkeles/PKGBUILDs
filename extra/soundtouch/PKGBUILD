# $Id: PKGBUILD 136035 2011-08-21 23:31:51Z eric $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - --with-pic for v6h/v7h
#  - patch to fix v5 softfp issues

pkgname=soundtouch
pkgver=1.8.0
pkgrel=1.1
pkgdesc="An audio processing library"
arch=('i686' 'x86_64')
url="http://www.surina.net/soundtouch/"
license=('LGPL')
depends=('gcc-libs')
source=(http://www.surina.net/soundtouch/${pkgname}-${pkgver}.tar.gz
        dont-use-integers-if-softfp.patch)
sha1sums=('2cee2c63194227b4a58da01a7cd92b8632f139cd'
          '893fafdca92cc48cd136ca05c05de0ff2063ebc9')

prepare() {
  cd ${pkgname}
  sed -i 's/AM_CONFIG_HEADER/AC_CONFIG_HEADER/' configure.ac
  patch -p1 -i ../dont-use-integers-if-softfp.patch
}

build() {
  cd ${pkgname}

  [ "${CARCH}" = "armv6h" -o "${CARCH}" = "armv7h" ] && CONFIGFLAG="--with-pic"
  ./bootstrap
  ./configure --prefix=/usr --enable-shared ${CONFIGFLAG}
  make
}

package() {
  cd ${pkgname}
  make DESTDIR="${pkgdir}" pkgdocdir=/usr/share/doc/soundtouch install
}
